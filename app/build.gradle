plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "pl.grzybdev.openmic.client"
        minSdk 19
        targetSdk 32
        multiDexEnabled true
        versionCode 12
        versionName "0.3.0.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "app"

    productFlavors {
        foss {
            dimension "app"
            applicationIdSuffix ".foss"
            versionNameSuffix "-foss"
            ext.useGoogleServices = false
        }

        google {
            dimension "app"
            ext.useGoogleServices = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

android.productFlavors.each { flavor ->
    if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains(flavor.name) && flavor.ext.useGoogleServices) {
        println("Building [${flavor.name}] flavor with Google Services - applying plugins")
        apply plugin: 'com.google.gms.google-services'
        apply plugin: 'com.google.firebase.crashlytics'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.material:material:1.7.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.1'
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.3"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3")
    implementation 'com.github.AppIntro:AppIntro:6.2.0'
    implementation "androidx.multidex:multidex:2.0.1"
    googleImplementation platform('com.google.firebase:firebase-bom:30.3.1')
    googleImplementation 'com.google.firebase:firebase-analytics-ktx'
    googleImplementation 'com.google.android.gms:play-services-ads:21.1.0'
    googleImplementation 'com.google.firebase:firebase-crashlytics-ktx'
}